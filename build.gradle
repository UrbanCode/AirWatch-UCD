import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'

defaultTasks 'distPlugin'

def buildLocaleDir = 'build/locale'
def buildUtilDir   = 'build/util'

configurations {
    runtime.exclude module: 'groovy-all'
}

repositories {
    mavenCentral()
    maven { url "https://public.dhe.ibm.com/software/products/UrbanCode/maven2/" }
}

dependencies {
    implementation 'com.ibm.urbancode.plugins:groovy-plugin-utils:+'
    implementation localGroovy()
    implementation 'com.ibm.urbancode.util:i18n-scraper:+'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    implementation group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
}

configurations {
    compileClasspath.extendsFrom implementation
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy', 'src/main/zip', "${buildDir}/util"]
        }
    }
    zip {
        groovy {
            srcDirs = ['src/main/zip']
        }
    }
    classes {
        groovy {
            srcDirs = ['src/main/groovy', "${buildDir}/util"]
        }
    }
}

compileJava {
    dependsOn 'extractGPU', 'copyi18n'
}
compileGroovy {
    include "i18n-scraper.groovy"
}

task copyDeps(type: Copy) {
    into 'lib'
    copy {
        from configurations.runtimeClasspath
        include { target ->
            return target.file.path.contains("com.ibm.urbancode")
        }
        into 'lib'
        rename { fileName ->
            stripVersion(fileName)
        }
    }
    copy {
        from configurations.runtimeClasspath
        exclude { target ->
            return target.file.path.contains("com.ibm.urbancode")
        }
        into 'lib'
    }
}

task extractGPU(type: Copy) {
    def zipFile = file('lib/groovy-plugin-utils.jar')
    def outputDir = file("${buildDir}/util")

    from zipTree(zipFile)
    into outputDir
}

task copyi18n(type: Copy) {
    def i18nFile = file('lib/i18n-scraper.groovy')
    def outputDir = file("${buildDir}/util")

    from i18nFile
    into outputDir
}

//extractGPU and copyi18n uses same lib so to ensure both waits for copyDeps
tasks.named("extractGPU") {
    dependsOn("copyDeps")
}

tasks.named("copyi18n") {
    dependsOn("copyDeps")
}
task distPlugin(type: Zip, dependsOn: ['compileGroovy', 'gatherI18n']) {
    def releaseVersion = apiVersion + "." + pluginVersion
    from(sourceSets.zip.groovy.srcDirs) {
        filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
    }
    into('lib') {
        from copyDeps
        exclude 'i18n-scraper.groovy'
        exclude 'groovy-plugin-utils.jar'
        exclude 'groovy-all-*.jar'
        exclude 'gson-*.jar'
    }
    into('locale') {
        from buildLocaleDir
    }
    into('classes') {
        from sourceSets.classes.groovy.srcDirs
        exclude 'META-INF', 'i18n-scraper.groovy'
    }
    archiveFileName = "${pluginName}-${releaseVersion}.zip"
}
task gatherI18n(type: JavaExec) {

    delete buildLocaleDir
    mkdir buildLocaleDir

    mainClass.set('i18n-scraper')

    args 'build/locale/en.properties'
    classpath = sourceSets.main.runtimeClasspath
}

task cleanAll(dependsOn: ['clean', 'cleanCopyDeps', 'cleanDistPlugin', 'cleanGatherI18n'])

String stripVersion(String fileNameWithVersion) {
    String ext = fileNameWithVersion.substring(fileNameWithVersion.lastIndexOf("."),fileNameWithVersion.length())
    int end = fileNameWithVersion.lastIndexOf("-"); //assumes that: name-version.ext. Will not work with name-version-SNAPSHOT.ext
    return fileNameWithVersion.substring(0, end) + ext
}
tasks.named('distPlugin') {
    dependsOn tasks.named('compileGroovy')
}
task runScript (dependsOn: 'classes', type: JavaExec) {
    mainClass.set('AirWatch-Ipa-upload')
    classpath = sourceSets.main.runtimeClasspath
    args "test/input.props", "test/output.props"
}
